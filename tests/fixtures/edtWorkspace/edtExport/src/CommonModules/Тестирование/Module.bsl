// Реализация MOCK-поведения для тестирования. Автор - Артур Аюханов (c) artbear

Функция РежимТестированияВключен() Экспорт
	Возврат ПараметрыСеанса.свзРежимТестированияВключен;
КонецФункции

Функция ВключитьРежимТестирования(структураПараметрыТестирования = Неопределено) Экспорт
	//Возврат свзТестирование.ВключитьРежимТестирования(структураПараметрыТестирования);
	
	Если НЕ РольДоступна("ПолныеПрава") И Метаданные.Роли.Количество() > 0 Тогда
		ВызватьИсключение "Не прав на включение режима тестирования";
	КонецЕсли;
	
	ИнициализироватьПеременныеТеста();
	
	Попытка
		Если Не РежимТестированияВключен() Тогда
			ПараметрыСеанса.свзРежимТестированияВключен = Истина;
			
			ЗаписьЖурналаРегистрации("РежимТестированияВключен.Установка",УровеньЖурналаРегистрации.Информация,,,"Установлен режим тестирования");
		КонецЕсли;
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации("РежимТестированияВключен.Установка",УровеньЖурналаРегистрации.Ошибка,,,"Не удалось установить режим тестирования: "+ТекстОшибки);
		
		ВызватьИсключение "Не удалось установить режим тестирования: "+ТекстОшибки;
	КонецПопытки;
	
	Если структураПараметрыТестирования <> Неопределено Тогда
		ПараметрыСеанса.свзПараметрыТестирования = Новый ХранилищеЗначения(структураПараметрыТестирования, Новый  СжатиеДанных(0)); //ФиксированнаяСтруктура(структураПараметрыТестирования);
		//глПараметрыТестирования = Новый Структура("МокОбъект", МокОбъект);
	КонецЕсли;
	Возврат Истина;
КонецФункции

Функция ОтключитьРежимТестирования() Экспорт
	//Возврат свзТестирование.ОтключитьРежимТестирования();
	
	Если НЕ РольДоступна("ПолныеПрава") И Метаданные.Роли.Количество() > 0 Тогда
		ВызватьИсключение "Не прав на включение/отключение режима тестирования";
	КонецЕсли;
	
	ИнициализироватьПеременныеТеста();
		
	Попытка
		Если РежимТестированияВключен() Тогда
			ПараметрыСеанса.свзРежимТестированияВключен = Ложь;
			ЗаписьЖурналаРегистрации("РежимТестированияВключен.Отмена",УровеньЖурналаРегистрации.Информация,,,"Отключен режим тестирования");
		КонецЕсли;       
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации("РежимТестированияВключен.Отмена",УровеньЖурналаРегистрации.Ошибка,,,"Не удалось отключить режим тестирования: "+ТекстОшибки);
		
		ВызватьИсключение "Не удалось отключить режим тестирования: "+ТекстОшибки;
	КонецПопытки;
	
	Возврат Истина;
КонецФункции


Функция ПолучитьПараметрыТестирования() Экспорт
	Если НЕ РежимТестированияВключен() Тогда
		ВызватьИсключение "Не включен режим тестирования";
	КонецЕсли;	
	Если ПараметрыСеанса.свзПараметрыТестирования = Неопределено Тогда
		ВызватьИсключение "Не заданы параметры тестирования";
	КонецЕсли;
	
	параметрыТестирования = ПараметрыСеанса.свзПараметрыТестирования.Получить();
	Если параметрыТестирования = Неопределено Тогда
		ВызватьИсключение "Не заданы параметры тестирования 2";
	КонецЕсли;
	
	Возврат параметрыТестирования;	
КонецФункции

Процедура ИнициализироватьПеременныеТеста() //Экспорт
	Попытка
		л = РежимТестированияВключен();
	Исключение
		ПараметрыСеанса.свзРежимТестированияВключен = Ложь;
		ПараметрыСеанса.свзПараметрыТестирования = Новый ХранилищеЗначения(Неопределено); //ФиксированнаяСтруктура(); //"МокОбъект", Неопределено);
		//глПараметрыТестирования = Неопределено;
	КонецПопытки;

КонецПроцедуры

// код написан для юнит-теста
&НаСервере
Функция ТестовыйВызов_ВстроенныйМетодБезПараметров() Экспорт
	Если свзТестирование.РежимТестированияВключен() Тогда
		параметрыТестирования = ПолучитьПараметрыТестирования();
		Возврат параметрыТестирования.Параметр;		
	КонецЕсли;
		//Если свзТестирование.РежимТестированияВключен() И ПараметрыСеанса.свзПараметрыТестирования <> Неопределено Тогда
		//	параметрыТестирования = ПараметрыСеанса.свзПараметрыТестирования.Получить();
		//	Возврат параметрыТестирования.Параметр;		
		//КонецЕсли;
	
	Возврат 2;
КонецФункции
