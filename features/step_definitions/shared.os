// Реализация шагов BDD-фич/сценариев c помощью фреймворка https://github.com/artbear/1bdd
#Использовать gitrunner
#Использовать asserts
#Использовать tempfiles

Перем БДД; //контекст фреймворка 1bdd
Перем ЭтоWindows;

// Метод выдает список шагов, реализованных в данном файле-шагов
Функция ПолучитьСписокШагов(КонтекстФреймворкаBDD) Экспорт
	БДД = КонтекстФреймворкаBDD;

	ВсеШаги = Новый Массив;

	ВсеШаги.Добавить("ЯУстанавливаюПутьВыполненияКомандыКТекущейБиблиотеке");
	ВсеШаги.Добавить("ЯСкопировалКаталогТестовогоХранилищаКонфигурацииВоВременныйКаталог");
	ВсеШаги.Добавить("ЯСохраняюЗначениеВременногоКаталогаВПеременной");
	ВсеШаги.Добавить("ЯСоздаюТестовойФайлAuthors");
	ВсеШаги.Добавить("ЯЗаписываюВФайлVersion");
	ВсеШаги.Добавить("ЯИнициализируюBareРепозиторийВоВременномКаталоге");
	ВсеШаги.Добавить("ЯИнициализируюСвязьСВнешнимРепозиторием");
	ВсеШаги.Добавить("ЯДобавляюПозиционныйПараметрДляКомандыИзПеременной");
	ВсеШаги.Добавить("ЯДобавляюПараметрДляКомандыИзПеременной");
	ВсеШаги.Добавить("ЯДобавляюПараметрыДляКоманды");
	ВсеШаги.Добавить("ЯСоздаюНеполныйТестовойФайлAuthors");
	ВсеШаги.Добавить("ЯСоздаюНовыйОбъектГитрепозиторий");
	ВсеШаги.Добавить("ЯУстанавливаюРабочейКаталогВоВременныйКаталог");
	ВсеШаги.Добавить("ЯВключаюПлагин");
	ВсеШаги.Добавить("ЯИнициализируюРепозиторийВКаталогеИзПеременной");
	ВсеШаги.Добавить("ЯВыключаюВсеПлагины");
	ВсеШаги.Добавить("ЯНаполняюBareРепозиторийИзПеременнойТестовымиДанными");
	ВсеШаги.Добавить("ЯУстанавливаюПеременнуюОкруженияИзПеременной");
	ВсеШаги.Добавить("ЯУстанавливаюРабочейКаталогИзПеременной");
	ВсеШаги.Добавить("ВКаталогеИзПеременнойСоздаетсяФайлИлиКаталог");
	ВсеШаги.Добавить("ВКаталогеИзПеременнойНеСоздаетсяФайлИлиКаталог");
	ВсеШаги.Добавить("ЯОчищаюЗначениеПеременныхОкружения");
	ВсеШаги.Добавить("ЯУстанавливаюТекущиеПлагины");
	ВсеШаги.Добавить("ЯСоздаюВременныйКаталогИСохраняюЕгоВПеременной");
	ВсеШаги.Добавить("ЯСкопировалКаталогТестовогоХранилищаКонфигурацииВКаталогИзПеременной");

	
	Возврат ВсеШаги;

КонецФункции

//Я создаю временный каталог и сохраняю его в переменной "КаталогПлагинов"
Процедура ЯСоздаюВременныйКаталогИСохраняюЕгоВПеременной(Знач ИмяПеременной) Экспорт

	ВременныйКаталог = ВременныеФайлы.СоздатьКаталог();
	
	БДД.СохранитьВКонтекст(ИмяПеременной, ВременныйКаталог);
	
КонецПроцедуры

//Я включаю плагин "limit"
Процедура ЯВключаюПлагин(Знач ПарамСтрока1) Экспорт
	
	Команда = Новый Команда;
	УстановитьДвижок(Команда);
	Команда.ДобавитьПараметр(ОбернутьВКавычки(ПутьКГитсинк()));
	Команда.ДобавитьПараметр("p e");
	Команда.ДобавитьПараметр(ПарамСтрока1);

	КодВозврата = Команда.Исполнить();
	Если Не КодВозврата = 0 Тогда
		ВызватьИсключение Команда.ПолучитьВывод();
	КонецЕсли;

КонецПроцедуры

//я скопировал каталог тестового хранилища конфигурации в каталог из переменной "КаталогХранилища1С"
Процедура ЯСкопировалКаталогТестовогоХранилищаКонфигурацииВКаталогИзПеременной(Знач ИмяПеременной) Экспорт
	КаталогХранилища1С = БДД.ПолучитьИзКонтекста(ИмяПеременной);
	КопироватьФайл(ПутьКВременномуФайлуХранилища1С(), ОбъединитьПути(КаталогХранилища1С, "1cv8ddb.1CD"));
КонецПроцедуры

//Я устанавливаю переменную окружения "GITSYNC_STORAGE_PATH" из переменной "КаталогХранилища1С"
Процедура ЯУстанавливаюПеременнуюОкруженияИзПеременной(Знач ИмяПеременной, Знач КаталогХранилища1С) Экспорт
	
	ЗначениеПеременной = БДД.ПолучитьИзКонтекста(КаталогХранилища1С);
	УстановитьПеременнуюСреды(ИмяПеременной, ЗначениеПеременной);

КонецПроцедуры

//Я устанавливаю рабочей каталог из переменной "ПутьКаталогаИсходников"
Процедура ЯУстанавливаюРабочейКаталогИзПеременной(Знач ПутьКаталогаИсходников) Экспорт
	ВременныйКаталог = БДД.ПолучитьИзКонтекста(ПутьКаталогаИсходников);
	
	УстановитьТекущийКаталог(ОбъединитьПути(ТекущийКаталог(), ВременныйКаталог));
КонецПроцедуры


//Я выключаю все плагины
Процедура ЯВыключаюВсеПлагины() Экспорт

	Команда = Новый Команда;
	УстановитьДвижок(Команда);
	Команда.ДобавитьПараметр(ОбернутьВКавычки(ПутьКГитсинк()));
	Команда.ДобавитьПараметр("p d -a");
	//Команда.ДобавитьПараметр(ПарамСтрока1);
	
	КодВозврата = Команда.Исполнить();
	Если Не КодВозврата = 0 Тогда
		ВызватьИсключение Команда.ПолучитьВывод();
	КонецЕсли;

КонецПроцедуры

Функция ИмяЛога() Экспорт
	Возврат "bdd.gitsync.feature";
КонецФункции


// Реализация шагов

// Процедура выполняется перед запуском каждого сценария
Процедура ПередЗапускомСценария(Знач Узел) Экспорт
	// ЯСоздаюНовыйОбъектГитрепозиторий()
КонецПроцедуры


//Я инициализирую репозиторий в каталоге из переменной "ПутьКаталогаИсходников"
Процедура ЯИнициализируюРепозиторийВКаталогеИзПеременной(Знач ПутьКаталогаИсходников) Экспорт
	ГитРепозиторий = БДД.ПолучитьИзКонтекста("ГитРепозиторий");
	ВременныйКаталог = БДД.ПолучитьИзКонтекста(ПутьКаталогаИсходников);
	ГитРепозиторий.УстановитьРабочийКаталог(ВременныйКаталог);

	ГитРепозиторий.Инициализировать();

КонецПроцедуры

//Я наполняю bare репозиторий из переменной "URLРепозитория" тестовыми данными
Процедура ЯНаполняюBareРепозиторийИзПеременнойТестовымиДанными(Знач ПеременнаяURLРепозитория) Экспорт

	ГитРепозиторий = БДД.ПолучитьИзКонтекста("ГитРепозиторий");
	URLРепозитория = БДД.ПолучитьИзКонтекста(ПеременнаяURLРепозитория);

	ВременныйКаталог = ВременныеФайлы.СоздатьКаталог();

	ГитРепозиторий.УстановитьРабочийКаталог(ВременныйКаталог);
	ГитРепозиторий.Инициализировать();
	readme = ОбъединитьПути(ВременныйКаталог, "README.md");
	ЗаписьТекста = Новый ЗаписьТекста(readme);
	ЗаписьТекста.ЗаписатьСтроку("TESTING");
	ЗаписьТекста.Закрыть();

	ПараметрыКоманды = Новый Массив;
	ПараметрыКоманды.Добавить("add");
	ПараметрыКоманды.Добавить("--all");
	ГитРепозиторий.ВыполнитьКоманду(ПараметрыКоманды);
	ГитРепозиторий.Закоммитить("init");
	
	НастройкаОтправить = Новый НастройкаКомандыОтправить;
	НастройкаОтправить.УстановитьURLРепозиторияОтправки(URLРепозитория);
	НастройкаОтправить.ОтображатьПрогресс();
	НастройкаОтправить.ПерезаписатьИсторию();
	НастройкаОтправить.Отслеживать();
	НастройкаОтправить.ПолнаяОтправка();
	
	ГитРепозиторий.УстановитьНастройкуКомандыОтправить(НастройкаОтправить);
	
	ГитРепозиторий.Отправить();

	ВременныеФайлы.БезопасноУдалитьФайл(ВременныйКаталог);

КонецПроцедуры

// Процедура выполняется после завершения каждого сценария
Процедура ПослеЗапускаСценария(Знач Узел) Экспорт
	ВременныеФайлы.Удалить();
КонецПроцедуры

//Я устанавливаю рабочей каталог во временный каталог
Процедура ЯУстанавливаюРабочейКаталогВоВременныйКаталог() Экспорт
	УстановитьТекущийКаталог(ВременныеФайлы.СоздатьКаталог());
КонецПроцедуры

//Я инициализирую bare репозиторий во временном каталоге
Процедура ЯИнициализируюBareРепозиторийВоВременномКаталоге() Экспорт
	ГитРепозиторий = БДД.ПолучитьИзКонтекста("ГитРепозиторий");
	ВременныйКаталог = БДД.ПолучитьИзКонтекста("ВременныйКаталог");
	ГитРепозиторий.УстановитьРабочийКаталог(ВременныйКаталог);
	ПараметрыКоманды = Новый Массив;
	ПараметрыКоманды.Добавить("init");
	ПараметрыКоманды.Добавить("--bare");
	ГитРепозиторий.ВыполнитьКоманду(ПараметрыКоманды);

КонецПроцедуры

//Я создаю новый объект ГитРепозиторий
Процедура ЯСоздаюНовыйОбъектГитрепозиторий() Экспорт
	ГитРепозиторий = Новый ГитРепозиторий;
	БДД.СохранитьВКонтекст("ГитРепозиторий", ГитРепозиторий);
КонецПроцедуры

//Я сохраняю значение временного каталога в переменной "URLРепозитория"
Процедура ЯСохраняюЗначениеВременногоКаталогаВПеременной(Знач ИмяПеременной) Экспорт
	ВременныйКаталог = БДД.ПолучитьИзКонтекста("ВременныйКаталог");
	БДД.СохранитьВКонтекст(ИмяПеременной, ВременныйКаталог);
КонецПроцедуры

//я инициализирую каталог исходников
Процедура ЯИнициализируюКаталогИсходников() Экспорт
	
	ВременныйКаталог = БДД.ПолучитьИзКонтекста("ВременныйКаталог");
	
	ПутьКаталогаИсходников = ВременныйКаталог;

	СоздатьКаталог(ПутьКаталогаИсходников);

	БДД.СохранитьВКонтекст("ПутьКаталогаИсходников",Новый Файл(ПутьКаталогаИсходников));

КонецПроцедуры

//Я создаю тестовой файл AUTHORS
Процедура ЯСоздаюТестовойФайлAuthors() Экспорт
	
	ПутьКаталогаИсходников = БДД.ПолучитьИзКонтекста("ПутьКаталогаИсходников");
	ФайлАвторов = Новый ЗаписьТекста;
	ФайлАвторов.Открыть(ОбъединитьПути(ПутьКаталогаИсходников, "AUTHORS"), "utf-8");
	ФайлАвторов.ЗаписатьСтроку("Администратор=Администратор <admin@localhost>");
	ФайлАвторов.ЗаписатьСтроку("Отладка=Отладка <debug@localhost>");
	ФайлАвторов.Закрыть();

КонецПроцедуры

//Я записываю "0" в файл VERSION
Процедура ЯЗаписываюВФайлVersion(Знач НомерВерсии) Экспорт
	
	ПутьКаталогаИсходников = БДД.ПолучитьИзКонтекста("ПутьКаталогаИсходников");
	
	ПутьКФайлуВерсий = ОбъединитьПути(ПутьКаталогаИсходников,"VERSION");
	Попытка
		Запись = Новый ЗаписьТекста(ПутьКФайлуВерсий, "utf-8");
		Запись.ЗаписатьСтроку("<?xml version=""1.0"" encoding=""UTF-8""?>");
		Запись.ЗаписатьСтроку("<VERSION>" + НомерВерсии + "</VERSION>");
		Запись.Закрыть();
	Исключение
		Если Запись <> Неопределено Тогда
			ОсвободитьОбъект(Запись);
		КонецЕсли;
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

//я инициализирую связь "ПутьКаталогаИсходников" с внешним репозиторием "URLРепозитория"
Процедура ЯИнициализируюСвязьСВнешнимРепозиторием(Знач ПеременнаяПутьКаталогаИсходников, Знач ПеременнаяURLРепозитория) Экспорт
	
	ГитРепозиторий = БДД.ПолучитьИзКонтекста("ГитРепозиторий");
	URLРепозитория = БДД.ПолучитьИзКонтекста(ПеременнаяURLРепозитория);
	ПутьКаталогаИсходников = БДД.ПолучитьИзКонтекста(ПеременнаяПутьКаталогаИсходников);
	
	ГитРепозиторий.УстановитьРабочийКаталог(ПутьКаталогаИсходников);
	ГитРепозиторий.Инициализировать();
	ПараметрыКоманды = Новый Массив;
	ПараметрыКоманды.Добавить("add");
	ПараметрыКоманды.Добавить("--all");
	ГитРепозиторий.ВыполнитьКоманду(ПараметрыКоманды);
	ГитРепозиторий.Закоммитить("тест");
	
	НастройкаОтправить = Новый НастройкаКомандыОтправить;
	НастройкаОтправить.УстановитьURLРепозиторияОтправки(URLРепозитория);
	НастройкаОтправить.ОтображатьПрогресс();
	НастройкаОтправить.ПерезаписатьИсторию();
	НастройкаОтправить.Отслеживать();
	НастройкаОтправить.ПолнаяОтправка();
	
	ГитРепозиторий.УстановитьНастройкуКомандыОтправить(НастройкаОтправить);
	
	ГитРепозиторий.Отправить();
	
КонецПроцедуры

//Я добавляю позиционный параметр для команды "gitsync" из переменной "URLРепозитория"
Процедура ЯДобавляюПозиционныйПараметрДляКомандыИзПеременной(Знач ИмяКоманды, Знач ИмяПеременной) Экспорт

	Команда = БДД.ПолучитьИзКонтекста(КлючКоманды(ИмяКоманды));
	ЗначениеПеременной = БДД.ПолучитьИзКонтекста(ИмяПеременной);
	
	Команда.ДобавитьПараметр(ЗначениеПеременной);

КонецПроцедуры

//Я добавляю параметр "-tmpdir" для команды "gitsync" из переменной "ВременнаяДиректория"
Процедура ЯДобавляюПараметрДляКомандыИзПеременной(Знач Параметр, Знач ИмяКоманды, Знач ИмяПеременной) Экспорт
	Команда = БДД.ПолучитьИзКонтекста(КлючКоманды(ИмяКоманды));
	ЗначениеПеременной = БДД.ПолучитьИзКонтекста(ИмяПеременной);
	Команда.ДобавитьПараметр(СтрШаблон("%1 %2", Параметр, ЗначениеПеременной))
КонецПроцедуры

//Я устанавливаю путь выполнения команды "gitsync" к текущей библиотеке
Процедура ЯУстанавливаюПутьВыполненияКомандыКТекущейБиблиотеке(Знач ИмяКоманды) Экспорт
	
	Команда = БДД.ПолучитьИзКонтекста(КлючКоманды(ИмяКоманды));
	УстановитьДвижок(Команда);
	
	Команда.ДобавитьПараметр(ОбернутьВКавычки(ПутьКГитсинк()));

КонецПроцедуры

//я скопировал каталог тестового хранилища конфигурации во временный каталог
Процедура ЯСкопировалКаталогТестовогоХранилищаКонфигурацииВоВременныйКаталог() Экспорт
	
	ВременныйКаталог = БДД.ПолучитьИзКонтекста("ВременныйКаталог");
	КопироватьФайл(ПутьКВременномуФайлуХранилища1С(),  ОбъединитьПути(ВременныйКаталог, "1cv8ddb.1CD"))

КонецПроцедуры

// И Я устанавливаю текущие плагины
Процедура ЯУстанавливаюТекущиеПлагины() Экспорт
	
	КаталогПлагинов = ВременныеФайлы.СоздатьКаталог();

	УстановитьПеременнуюСреды("GITSYNC_PLUGINS_PATH", КаталогПлагинов);
	
	КомандаOpm = Новый Команда;
	КомандаOpm.УстановитьРабочийКаталог(КаталогГитсинк());
	КомандаOpm.УстановитьКоманду("opm");
	КомандаOpm.ДобавитьПараметр("run testing-build");	

	КодВозврата = КомандаOpm.Исполнить();

	Если КодВозврата <> 0  Тогда
		ВызватьИсключение КомандаOpm.ПолучитьВывод();
	КонецЕсли;

КонецПроцедуры

//Я создаю неполный тестовой файл AUTHORS
Процедура ЯСоздаюНеполныйТестовойФайлAuthors() Экспорт
	
	ПутьКаталогаИсходников = БДД.ПолучитьИзКонтекста("ПутьКаталогаИсходников");
	ФайлАвторов = Новый ЗаписьТекста;
	ФайлАвторов.Открыть(ОбъединитьПути(ПутьКаталогаИсходников, "AUTHORS"), "utf-8");
	ФайлАвторов.ЗаписатьСтроку("Отладка=Отладка <debug@localhost>");
	ФайлАвторов.Закрыть();

КонецПроцедуры

//Я добавляю параметры для команды "gitsync"
//|--storage-user Администратор|
//|-useVendorUnload|
Процедура ЯДобавляюПараметрыДляКоманды(Знач ИмяКоманды, Знач ТаблицаПараметров) Экспорт
	
	Команда = БДД.ПолучитьИзКонтекста(КлючКоманды(ИмяКоманды));
	Для Каждого Параметр из ТаблицаПараметров Цикл
		Команда.ДобавитьПараметр(Параметр[0])
	КонецЦикла

КонецПроцедуры

//В каталоге из переменной "ПутьКаталогаИсходников" создается файл или каталог "AUTHORS"
Процедура ВКаталогеИзПеременнойСоздаетсяФайлИлиКаталог(Знач ПутьКаталогаИсходников, Знач ИмяФайла) Экспорт
	
	ВременныйКаталог = БДД.ПолучитьИзКонтекста(ПутьКаталогаИсходников);
	ИскомыйФайл = Новый Файл(ОбъединитьПути(ВременныйКаталог, ИмяФайла));
	Ожидаем.Что(ИскомыйФайл.Существует(), "Файл должен был существовать").ЭтоИстина();

КонецПроцедуры

//В каталоге из переменной "ПутьКаталогаИсходников" не создается файл или каталог ".git"
Процедура ВКаталогеИзПеременнойНеСоздаетсяФайлИлиКаталог(Знач ПутьКаталогаИсходников, Знач ИмяФайла) Экспорт
	ВременныйКаталог = БДД.ПолучитьИзКонтекста(ПутьКаталогаИсходников);
	ИскомыйФайл = Новый Файл(ОбъединитьПути(ВременныйКаталог, ИмяФайла));
	Ожидаем.Что(ИскомыйФайл.Существует(), "Файл не должен был существовать").ЭтоЛожь();
КонецПроцедуры

//Я очищаю значение переменных окружения
//|GITSYNC_STORAGE_PATH|
//|GITSYNC_WORKDIR|
Процедура ЯОчищаюЗначениеПеременныхОкружения(Знач ПарамТаблица1) Экспорт
	Для каждого Переменная Из ПарамТаблица1 Цикл
		УстановитьПеременнуюСреды(Переменная[0], "");
	КонецЦикла;
КонецПроцедуры

Функция ПутьКВременномуФайлуХранилища1С()
	
	Возврат ОбъединитьПути(КаталогFixtures(), "ТестовыйФайлХранилища1С.1CD");
	
КонецФункции

Функция КаталогFixtures()
	Возврат ОбъединитьПути(КаталогГитсинк(), "tests", "fixtures");
КонецФункции

Функция КаталогГитсинк()
	Возврат ОбъединитьПути(ТекущийСценарий().Каталог, "..", "..");
КонецФункции

Функция ПутьКГитсинк()
	
	Возврат ОбъединитьПути(КаталогГитсинк(), "bin", "gitsync/src/cmd/gitsync.os");

КонецФункции

Функция ОбернутьВКавычки(Знач Строка);
	Возврат """" + Строка + """";
КонецФункции

Функция КлючКоманды(Знач ИмяКоманды)
	Возврат "Команда-" + ИмяКоманды;
КонецФункции

Функция ЭтоWindows()
	Если ЭтоWindows = Неопределено Тогда
		СистемнаяИнформация = Новый СистемнаяИнформация;
		ЭтоWindows = Найти(НРег(СистемнаяИнформация.ВерсияОС), "windows") > 0;
	КонецЕсли;
	Возврат ЭтоWindows;
КонецФункции

Процедура УстановитьДвижок(Команда)
	Команда.УстановитьКоманду("oscript");
	Если Не ЭтоWindows() Тогда
		Команда.ДобавитьПараметр("-encoding=utf-8");	
	КонецЕсли;
КонецПроцедуры

ЭтоWindows = ЭтоWindows();

Лог = Логирование.ПолучитьЛог(ИмяЛога());
